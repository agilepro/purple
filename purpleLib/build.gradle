/*
 build for the purple utilities library
 */
 
plugins {
    // the "id" commands below find an existing pluging code, 
    // create an instance of it, and put into the project.
    id 'java-library';
    id 'maven-publish';
    id 'signing';
}
java {
    withJavadocJar();
    withSourcesJar();
}
project.repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral();
}; 

project.version '3.0';
project.description = "Purple utilities for web programming";
project.group = 'com.purplehillsbooks.purple';
project.archivesBaseName = 'purple';

project.dependencies({

    // Use JUnit test framework.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    
    compileOnly 'org.apache.tomcat:tomcat-catalina:8.5.46'
})


project.test {
    useJUnitPlatform()
}


//this turns off all the warnings in the JavaDoc
javadoc.options.addStringOption('Xdoclint:none', '-quiet');


//java source files are UTF-8
//apply plugin: "java"
project.tasks.withType(JavaCompile, {
    options.encoding = "UTF-8"
});


publishing {
    publications{

        mavenJava(MavenPublication){

            groupId = 'com.purplehillsbooks.purple'
            artifactId = 'purple'
            version = '3.0'
            from components.java

            pom {
                name = 'purple'
                description = 'A set of useful utility classes for web applications.'
                url = 'https://github.com/agilepro/purple'
                inceptionYear = '2017'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'kswenson'
                        name = 'Keith Swenson'
                        email = 'gradle@purplehillsbooks.com'
                    }
                }
                scm {
                    connection='scm:git:git:github.com/agilepro/purple.git'
                    developerConnection='scm:git:https://github.com/agilepro/purple.git'
                    url='https://github.com/agilepro/purple'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}




//generate some output metadata
project.plugins.each {
   println "PLUGIN: "+ it
}

project.repositories.each {
   println "REPO: "+ it
}
